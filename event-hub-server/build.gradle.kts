plugins {
	java
	war
	id("org.asciidoctor.jvm.convert") version "3.3.2"
	id("org.springframework.boot") version "3.2.3"
	id("org.flywaydb.flyway") version "10.10.0"
	id("com.diffplug.spotless") version "6.25.0"
}

apply(plugin = "io.spring.dependency-management")

group = "com.vf"
version = "0.0.1-SNAPSHOT"

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

springBoot  {
    buildInfo()
}

configurations {
	compileOnly {
		extendsFrom(configurations.annotationProcessor.get())
	}
}

repositories {
	mavenCentral()
}

val snippetsDir by extra { file("build/generated-snippets") }
val asciidoctorExt by configurations.creating


dependencies {
	providedCompile(platform(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES))
	implementation ("com.google.code.findbugs:jsr305:3.0.2")
	implementation("org.springframework.data:spring-data-rest-hal-explorer")
	asciidoctorExt("org.springframework.restdocs:spring-restdocs-asciidoctor")
	implementation("org.springframework.boot:spring-boot-starter-actuator")
	implementation("org.springframework.boot:spring-boot-starter-data-jpa")
	implementation("org.springframework.boot:spring-boot-starter-validation")
	implementation("org.springframework.boot:spring-boot-starter-web")
	implementation("org.springframework.boot:spring-boot-starter-webflux")
	implementation("org.springframework.session:spring-session-core")
	implementation ("org.mapstruct:mapstruct:1.5.5.Final")
	annotationProcessor ("org.mapstruct:mapstruct-processor:1.5.5.Final")
	implementation ("org.flywaydb:flyway-core")
	implementation("org.springframework.boot:spring-boot-devtools")
	runtimeOnly("com.h2database:h2")
	providedRuntime("org.springframework.boot:spring-boot-starter-tomcat")
	testImplementation("org.springframework.boot:spring-boot-starter-test")
	testImplementation("io.projectreactor:reactor-test")
	testImplementation("org.springframework.restdocs:spring-restdocs-mockmvc")
	testAnnotationProcessor("org.mapstruct:mapstruct-processor:1.5.5.Final")
}

spotless {
  java {
    // Target all Java source files
    target("src/**/*.java")
    
    // Optional: Choose your formatter (replace with your preference)
    googleJavaFormat() // Uses Google Java Format
    
    // Remove unused imports (optional)
    removeUnusedImports()
  }
}

asciidoctorj {
	fatalWarnings(listOf(missingIncludes()))
}

tasks.withType<JavaCompile>().configureEach{
    options.encoding = "UTF-8"
}

tasks.withType<Javadoc>().configureEach{
    options.encoding = "UTF-8"
}

tasks.test {
	systemProperty("file.encoding", "UTF-8")
	useJUnitPlatform()
	outputs.dir(snippetsDir)
}

tasks.asciidoctor {
	inputs.dir(snippetsDir)
	configurations(listOf(asciidoctorExt))
	dependsOn("test")
	attributes(mapOf("snippets" to snippetsDir))
	outputOptions{
		backends ("html5")
	}
	options(mapOf("doctype" to "book"))
}


springBoot{
	mainClass.set("com.vf.eventhubserver.EventhubServerApplication")
}

tasks.jar {
	dependsOn("asciidoctor")
	from ("${snippetsDir}/html5") {
		into ("static/docs")
	}
}

tasks.war {
	dependsOn("asciidoctor")
	from ("${snippetsDir}/html5") {
		into ("static/docs")
	}
}

// A custom task to delete all the files and folders generated by Gradle
tasks.register<Delete>("clearAll") {
	delete("bin")
	delete (".classpath")
	delete (".gradle")
	delete (".nb-gradle")
	delete (".project")
	delete (".settings")
	delete (".vscode")
	delete (".DS_Store")
	delete (".idea")
    dependsOn(tasks.clean)
}

